// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Keyboard.jack

/**
 * A library for handling user input from the keyboard.
 */
class Keyboard {

    static int _memoryMapAddress;

    /** Initializes the keyboard. */
    function void init() {
        let _memoryMapAddress = 24576;

        return;
    } 

    /**
     * Returns the character of the currently pressed key on the keyboard;
     * if no key is currently pressed, returns 0.
     *
     * Recognizes all ASCII characters, as well as the following keys:
     * new line = 128 = String.newline()
     * backspace = 129 = String.backspace()
     * left arrow = 130
     * up arrow = 131
     * right arrow = 132
     * down arrow = 133
     * home = 134
     * End = 135
     * page up = 136
     * page down = 137
     * insert = 138
     * delete = 139
     * ESC = 140
     * F1 - F12 = 141 - 152
     */
    function char keyPressed() {
        return Memory.peek(_memoryMapAddress);
    }

    /**								
     * Waits until a key is pressed on the keyboard and released,
     * then echoes the key to the screen, and returns the character 
     * of the pressed key.
     */
    function char readChar() {
        var char asciiCode, key;

        // wait till a key is pressed
        while (key = 0) {
            let key = Keyboard.keyPressed();
        }

        let asciiCode = key;

        // wait untill the key is released
        while (~(key = 0)) {
            let key = Keyboard.keyPressed();
        }

        do Output.printChar(asciiCode);

        return asciiCode;
    }

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its value. Also handles user backspaces.
     */
    function String readLine(String message) {
        var char asciiCode;
        var String userInput;
        var boolean pressedEnter;

        let pressedEnter = false;
        let asciiCode = 0;
        let userInput = String.new(16); // hardcoding String max length, based on tests in Main.jack
        
        do Output.printString(message);

        while (~pressedEnter) {
            let asciiCode = Keyboard.readChar();

            // ASCII Code for newLine is 128
            if (asciiCode = 128) {
                let pressedEnter = true;
            }
            else {
                // ASCII Code for newLine is 129
                if (asciiCode = 129) {
                    do userInput.eraseLastChar();
                }
                else {
                    do userInput.appendChar(asciiCode);
                }
            }
        }

        return userInput;
    }   

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its integer value (until the first non-digit character in the
     * entered text is detected). Also handles user backspaces. 
     */
    function int readInt(String message) {
        var String userInput;

        let userInput = Keyboard.readLine(message);

        return userInput.intValue();
    }
}
