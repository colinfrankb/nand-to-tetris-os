// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Math.jack

/**
 * A library of commonly used mathematical functions.
 * Note: Jack compilers implement multiplication and division using OS method calls.
 */
class Math {

    static Array twoToThe;
    static int bitLength;

    /** Initializes the library. */
    function void init() {
        var int counter, shiftedSum;
        
        let bitLength = 16;
        let counter = 0;
        let shiftedSum = 1;
        let twoToThe = Array.new(bitLength);

        while(counter < bitLength) {
            let twoToThe[counter] = shiftedSum;          
            let shiftedSum = shiftedSum + shiftedSum;
            let counter = counter + 1;
        }

        return;      
    }

    /** Returns the absolute value of x. */
    function int abs(int x) {
        if (x < 0) {
            let x = x * -1;
        }

        return x;
    }

    /** Returns the product of x and y. 
     *  When a Jack compiler detects the multiplication operator '*' in the 
     *  program's code, it handles it by invoking this method. In other words,
     *  the Jack expressions x*y and multiply(x,y) return the same value.
     */
    function int multiply(int x, int y) {
        var int sum, shiftedX, counter;

        // do Output.printInt(y);

        let sum = 0;
        let shiftedX = x;
        let counter = 0;

        //loop through the bits of y
        while(counter < bitLength) {
            if(~((y & twoToThe[counter]) = 0)) { // if the bit is set i.e 1
                let sum = sum + shiftedX; // add the multiple of the bit value
            }
            let shiftedX = shiftedX + shiftedX; // multiply by two because the next bit value will be the current
                                     // bit value multiplied by 2
                                     // i.e x * b^n-1, where b is the base
                                     // therefore, x * 2^counter
            let counter = counter + 1;
        }

        return sum;
    }

    /** Returns the integer part of x/y.
     *  When a Jack compiler detects the multiplication operator '/' in the 
     *  program's code, it handles it by invoking this method. In other words,
     *  the Jack expressions x/y and divide(x,y) return the same value.
     */
    function int divide(int x, int y) {
        var int q, absoluteX, absoluteY, arithmeticNegate;

        let absoluteX = Math.abs(x);
        let absoluteY = Math.abs(y);

        if (~(absoluteX = x) | ~(absoluteY = y)) {
          let arithmeticNegate = -1;
        }
        else {
          let arithmeticNegate = 1;
        }

        if (absoluteY > absoluteX) {
          return 0;
        }

        let q = Math.divide(absoluteX, absoluteY * 2);
        
        if ((absoluteX - (2 * q * absoluteY)) < absoluteY) { // y can't go into x
          return (2 * q) * arithmeticNegate;
        }
        else { // y can still go into x
          return ((2 * q) + 1) * arithmeticNegate;
        }
    }

    /** Returns the integer part of the square root of x. */
    function int sqrt(int x) {
        var int y, counter, powerResult;

        let y = 0;
        let counter = (bitLength / 2) - 1;

        while (counter > -1) {
          let powerResult = Math.power((y + Math.power(2, counter)), 2);
          if ((powerResult < x) | (powerResult = x)) {
            if (powerResult > 0) {
              let y = y + Math.power(2, counter);
            }
          }

          let counter = counter - 1;
        }

        return y;
    }

    function int power(int x, int exponent) {
      var int result;
      
      if (exponent = 0)
      {
        return 1;
      }

      let result = x;

      while (exponent > 1) {
        let result = result * x;
        let exponent = exponent - 1;
      }

      return result;
    }

    /** Returns the greater number. */
    function int max(int a, int b) {
        if (a > b) {
          return a;
        }

        return b;
    }

    /** Returns the smaller number. */
    function int min(int a, int b) {
        if (a < b) {
          return a;
        }

        return b;
    }
}
